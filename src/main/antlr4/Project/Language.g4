grammar Language;
program: (statement)+;

// âœ… - Done
// ğŸ†— - Skipped, should be fine

statement                                                                                  // Type Check | Tests
    : ';'                                                             # emptyStatement     // ğŸ†—           âœ…
    | TYPE VAR (',' VAR)* ';'                                         # varDeclStatement   // âœ…           âœ…
    | expression ';'                                                  # exprStatement      // ğŸ†—           âœ…
    | 'read' VAR (',' VAR)* ';'                                       # readStatement      // âœ…           âœ…
    | 'write' expression (',' expression)* ';'                        # writeStatement     // ğŸ†—ï¸           âœ…
    | '{' statement* '}'                                              # blockStatement     // ğŸ†—           âœ…
    | 'if' '(' condition ')' statement ('else' statement)?            # ifStatement        // ğŸ†—           âœ…
    | 'while' '(' condition ')' statement                             # whileStatement     // ğŸ†—           âœ…
    | 'do' statement 'while' '(' condition ')' ';'                    # doWhileStatement   // ğŸ†—           ğŸ†—
    | 'for' '(' expression ';' condition ';' expression ')' statement # forStatement       // ğŸ†—           ğŸ†—
    ;

condition                           // Type Check | Tests
	: expression # boolCondition    // âœ…
	;

expression                                                                       // Type Check | Tests
    : '(' expression ')'    			                  # parenExpression      // ğŸ†—           ğŸ†—
    | expression op=('+' | '-' | '*' | '/') expression    # aritmExpression      // âœ…           âœ…
    | expression op='%' expression                        # moduloExpression     // âœ…           âœ…
    | expression op='.' expression                        # concatExpression     // âœ…           âœ…
    | expression op=('<' | '>') expression                # relationExpression   // âœ…           âœ…
    | expression op=('==' | '!=') expression              # comparisonExpression // âœ…           âœ…
    | expression op=('&&' | '||') expression              # logicExpression      // âœ…           âœ…
    | expression '?' expression split=':' expression      # ternaryExpression    // âœ…           âœ…
    | op='!' expression                                   # notExpression        // âœ…           âœ…
    | op='-' expression                                   # negExpression        // âœ…           âœ…
    | VAR                                                 # varExpression        // âœ…           âœ…
    | literal                                             # literalExpression    // âœ…           âœ…
    | VAR '=' expression                                  # assignExpression     // âœ…           âœ…
    ;

literal
    : INT
    | FLOAT
    | BOOL
    | STRING
    ;

INT: DIGIT+;
FLOAT: DIGIT+ '.' DIGIT*;
BOOL: 'true' | 'false';
STRING: '"' (ESC | ~["\\])* '"';

TYPE: 'int' | 'float' | 'bool' | 'string';
VAR: LETTER (LETTER | DIGIT)*;

COMMENT: '//' ~[\r\n]* -> skip;
SPACE: [ \t\r\n]+ -> skip;

fragment ESC: '\\' (["\\/bfnrt] | UNICODE_ESC);
fragment UNICODE_ESC: 'u' HEX HEX HEX HEX;
fragment HEX: [0-9a-fA-F];

fragment LETTER: [a-zA-Z];
fragment DIGIT: [0-9];
