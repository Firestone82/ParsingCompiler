grammar Language;
program: (statement)+;

// âœ… - Done
// ğŸ†— - Skipped, should be fine

statement                                                                                  // Type Check | Tests
    : ';'                                                             # emptyStatement     // ğŸ†—ï¸           âœ…âœ…
    | TYPE VAR (',' VAR)* ';'                                         # varDeclStatement   // âœ…           âœ…âœ…
    | expression ';'                                                  # exprStatement      // âœ…           âœ…âœ…
    | 'read' VAR (',' VAR)* ';'                                       # readStatement      // âœ…           âœ…âœ…
    | 'write' expression (',' expression)* ';'                        # writeStatement     // ğŸ†—ï¸           âœ…âœ…
    | '{' statement* '}'                                              # blockStatement     // ğŸ†—           âœ…âœ…
    | 'if' '(' condition ')' statement ('else' statement)?            # ifStatement        // ğŸ†—           âœ…
    | 'while' '(' condition ')' statement                             # whileStatement     // ğŸ†—           âœ…
    | 'do' statement 'while' '(' condition ')' ';'                    # doWhileStatement   // ğŸ†—           âœ…
    | 'for' '(' expression ';' condition ';' expression ')' statement # forStatement       // ğŸ†—           âœ…
    ;

condition                           // Type Check | Tests
	: expression # boolCondition    // âœ…
	;

expression                                                                       // Type Check | Tests
    : '(' expression ')'    			                  # parenExpression      // âœ…           âœ…âœ…
    | expression op=('+' | '-' | '*' | '/') expression    # aritmExpression      // âœ…           âœ…âœ…
    | expression op='%' expression                        # moduloExpression     // âœ…           âœ…âœ…
    | expression op='.' expression                        # concatExpression     // âœ…           âœ…âœ…
    | expression op=('<' | '>') expression                # relationExpression   // âœ…           âœ…âœ…
    | expression op=('==' | '!=') expression              # comparisonExpression // âœ…           âœ…âœ…
    | expression op=('&&' | '||') expression              # logicExpression      // âœ…           âœ…âœ…
    | expression '?' expression split=':' expression      # ternaryExpression    // âœ…           âœ…
    | op='!' expression                                   # notExpression        // âœ…           âœ…âœ…
    | op='-' expression                                   # negExpression        // âœ…           âœ…âœ…
    | VAR                                                 # varExpression        // âœ…           ğŸ†—ğŸ†—
    | literal                                             # literalExpression    // âœ…           ğŸ†—ğŸ†—
    | VAR '=' expression                                  # assignExpression     // âœ…           âœ…âœ…
    ;

literal
    : INT
    | FLOAT
    | BOOL
    | STRING
    ;

INT: DIGIT+;
FLOAT: DIGIT+ '.' DIGIT*;
BOOL: 'true' | 'false';
STRING: '"' (ESC | ~["\\])* '"';

TYPE: 'int' | 'float' | 'bool' | 'string';
VAR: LETTER (LETTER | DIGIT)*;

COMMENT: '//' ~[\r\n]* -> skip;
SPACE: [ \t\r\n]+ -> skip;

fragment ESC: '\\' (["\\/bfnrt] | UNICODE_ESC);
fragment UNICODE_ESC: 'u' HEX HEX HEX HEX;
fragment HEX: [0-9a-fA-F];

fragment LETTER: [a-zA-Z];
fragment DIGIT: [0-9];
